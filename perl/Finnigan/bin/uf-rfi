#!/usr/bin/env perl

use strict;
use warnings;
use feature qw/state say/;
use 5.010;

use Getopt::Long;
use Pod::Usage;
use Finnigan;

my $option_help = 0;
my $option_man = 0;
my $option_dump = 0;
my $option_html = 0;
my $option_wiki = 0;
my $option_size = 0;
my $option_preamble = 0;
my $option_relative = 0;

Getopt::Long::Configure ("bundling");
GetOptions(
	   'help|?' => \$option_help,
	   'man' => \$option_man,
	   'dump|d' => \$option_dump,
	   'size|s' => \$option_size,
	   'html|h' => \$option_html,
	   'wiki|w' => \$option_wiki,
	   'preamble|p' => \$option_preamble,
	   'relative|r' => \$option_relative,
	  ) or pod2usage(2);
pod2usage(1) if $option_help;
pod2usage(-existstatus => 0, -verbose => 2) if $option_man;

@ARGV == 1 or do{ say STDERR "Expecting a single input file\n"; pod2usage(2) };

my $file = shift @ARGV;

-e $file or die "file '$file' does not exist";
-f $file or die "'$file' is not a plain file";
-s $file or die "'$file' has zero size";

# -----------------------------------------------------------------------------
open INPUT, "<$file" or die "can't open '$file': $!";
binmode INPUT;

my $header = Finnigan::FileHeader->decode(\*INPUT);
my $seq_row = Finnigan::SeqRow->decode(\*INPUT, $header->version);
my $cas_info = Finnigan::CASInfo->decode(\*INPUT);
my $rfi = Finnigan::RawFileInfo->decode(\*INPUT, $header->version);

if ( $option_size ) {
  if ( $option_preamble ) {
    say "size: " . $rfi->preamble->size;
  }
  else {
    say "size: " . $rfi->size;
  }
}

if ( $option_dump ) {
  if ( $option_html ) {
    if ( $option_preamble ) {
      $rfi->preamble->dump(style => 'html', relative => $option_relative);
    }
    else {
      $rfi->dump(style => 'html', relative => $option_relative);
    }
  }
  elsif ( $option_wiki ) {
    if ( $option_preamble ) {
      $rfi->preamble->dump(style => 'wiki', relative => $option_relative);
    }
    else {
      $rfi->dump(style => 'wiki', relative => $option_relative);
    }
  }
  else {
    if ( $option_preamble ) {
      $rfi->preamble->dump(relative => $option_relative);
    }
    else {
      $rfi->dump(relative => $option_relative);
    }
  }
}
else {
  say "$file: "
    . $rfi->preamble->timestamp . "; "
      . "RunHeader addr: "
	. $rfi->preamble->run_header_addr
	  ;
}

__END__
=head1 NAME

uf-header - decode the file header in a Finnigan raw file

=head1 SYNOPSIS

uf-header [options] file

 Options:
   --help            brief help message
   --man             full documentation
   --html            requet html formatting of the output of -dump 
   --size            tell object size
   --preamble        dump the content of RawFileInfoPreamble instead of the RawFileInfo itself
   --relative        show relative addresses

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exits.

=item B<--man>

Prints the manual page and exits.

=item B<--dump>

Prints the table listing all header fields with their seek addresses,
sizes, acess keys and values.

=item B<--html>

Changes the output style of -dump to html.

=item B<--size>

Prints the size of the header in bytes.

=item B<--preamble>

Dump the contents of RawFileInfoPreamble, instead of the parent object.

=item B<--relative>

Show relative addresses of all itmes. The default is to show the
absolute seek address.

=back

=head1 DESCRIPTION

B<uf-rfi> will read the given input file and display the contents
of its RawFileInfo structure, or its component RawFileInfoPreamble.

It will return an error message if its input is not a Finnigan raw
file.

By default, it dumps the object in a tabular format.

=cut
